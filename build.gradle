apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

version = '0.1.0'
group = 'net.alchim31.jmex'

sourceCompatibility = '1.6'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    jstackalloc
}

dependencies {
    jstackalloc 'net.sf.sociaal:stack-alloc:3.0.0.20130526'
    jstackalloc 'asm:asm-all:3.1'
    compile 'java3d:vecmath:1.3.1'
    compile 'net.sf.sociaal:stack-alloc:3.0.0.20130526'
    compile 'com.jme3:jme3-core:3.0.10-pre-alpha-svn'
    compile 'com.jme3:jme3-terrain:3.0.10-pre-alpha-svn'
    testCompile 'org.lwjgl.lwjgl:lwjgl:2.9.1'
    testCompile 'org.lwjgl.lwjgl:lwjgl_util:2.9.1'
    testCompile 'org.swinglabs:swing-layout:1.0.3'

}

task jstackalloc(dependsOn: 'compileJava') << {
	//println sourceSets.main.output.classesDir
    ant.taskdef(name: 'instrument_stack', classname: 'cz.advel.stack.instrument.InstrumentationTask') {
       classpath {
         pathelement(path: configurations.jstackalloc.asPath)
         pathelement(path: configurations.compile.asPath)
         pathelement(location: sourceSets.main.output.classesDir)
       }
    }
    ant.instrument_stack(dest: sourceSets.main.output.classesDir, packageName:"com.bulletphysics", isolated:"true") {
       fileset(dir: sourceSets.main.output.classesDir, includes:"**/*.class")
    }
}

classes.dependsOn jstackalloc